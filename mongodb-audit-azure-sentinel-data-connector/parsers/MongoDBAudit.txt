// Usage Instructions: 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as MongoDBAudit and specifying Legacy Category.
// This function maps MongoDB Audit events to Azure Sentinel Information Model (ASIM) (https://docs.microsoft.com/azure/sentinel/normalization).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. MongoDBAudit | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
MongoDBAudit_CL
| extend EventVendor = 'MongoDB',
         EventProduct = 'MongDB Audit',
         EventCount = 1
| extend d=parse_json(RawData)
| extend EventEndTime = todatetime(d['ts']['$date'])
| extend DvcAction = d['atype']
| extend SrcIpAddr = d['remote']['ip']
| extend SrcPortNumber = d['remote']['port']
| extend DstIpAddr = d['local']['ip']
| extend DstPortNumber = d['local']['port']
| extend Users = d['users']
| extend Roles = d['roles']
| extend Parameters = d['param']
| extend EventResultCode = d['result']
| extend EventResult = iff(EventResultCode==0,"Success", iif(EventResultCode==18,"Authentication Failed", iif(EventResultCode==334,"Mechanism Unavailable",iif(EventResultCode==13,"Unauthorized to perform the operation.",iif(EventResultCode==276,"Index build aborted.",iif(EventResultCode==26,"NamespaceNotFound",''))))))
| project-away d, RawData